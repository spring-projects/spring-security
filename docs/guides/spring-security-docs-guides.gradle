apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'io.spring.convention.repository'

asciidoctor {
	baseDir = file('src/docs/asciidoc')
	options eruby: 'erubis'

	def ghTag = snapshotBuild ? 'main' : project.version
	def ghSamplesUrl = "https://github.com/spring-projects/spring-security-samples/tree/$ghTag"
	attributes copycss : '',
			icons : 'font',
			'source-highlighter': 'prettify',
			sectanchors : '',
			toc: '',
			'toc-placement' : 'preamble',
			idprefix: '',
			idseparator: '-',
			doctype: 'book',
			'spring-security-version' : project.version,
			'download-url' : getDownloadUrl(),
			'include-maven-repository' : getMavenRepositoryInclude(),
			revnumber : project.version,
			'gh-samples-url': ghSamplesUrl,
			'gh-old-samples-url': "https://github.com/spring-projects/spring-security/tree/5.4.x/samples"
}

ext.spec = copySpec {
	into ('guides') {
		from(asciidoctor.outputDir)
		exclude 'build', 'Guardfile'
	}
}

def getDownloadUrl() {
	snapshotBuild ? "https://github.com/spring-projects/spring-security/archive/main.zip" : "https://github.com/spring-projects/spring-security/archive/${project.version}.zip"
}


def getMavenRepositoryInclude() {
	if(snapshotBuild) {
		return "_includes/maven-repository-snapshot.asc"
	} else if(releaseBuild) {
		return "_includes/maven-repository-release.asc"
	} else {
		return "_includes/maven-repository-milestone.asc"
	}
}
