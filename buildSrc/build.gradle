plugins {
	id "java-gradle-plugin"
	id "java"
	id "groovy"
	id 'com.apollographql.apollo' version '2.4.5'
}


sourceCompatibility = 1.8

repositories {
	jcenter()
	gradlePluginPortal()
	mavenCentral()
	maven { url 'https://repo.spring.io/plugins-release/' }
}

sourceSets {
	main {
		java {
			srcDirs = []
		}
		groovy {
			srcDirs += ["src/main/java"]
		}
	}
}

gradlePlugin {
	plugins {
		trang {
			id = "trang"
			implementationClass = "trang.TrangPlugin"
		}
		locks {
			id = "locks"
			implementationClass = "lock.GlobalLockPlugin"
		}
		managementConfiguration {
			id = "io.spring.convention.management-configuration"
			implementationClass = "io.spring.gradle.convention.ManagementConfigurationPlugin"
		}
		updateDependencies {
			id = "org.springframework.security.update-dependencies"
			implementationClass = "org.springframework.security.convention.versions.UpdateDependenciesPlugin"
		}
		sagan {
			id = "org.springframework.security.sagan"
			implementationClass = "org.springframework.gradle.sagan.SaganPlugin"
		}
		githubMilestone {
			id = "org.springframework.github.milestone"
			implementationClass = "org.springframework.gradle.github.milestones.GitHubMilestonePlugin"
		}
		githubChangelog {
			id = "org.springframework.github.changelog"
			implementationClass = "org.springframework.gradle.github.changelog.GitHubChangelogPlugin"
		}
	}
}

configurations {
	implementation {
		exclude module: 'groovy-all'
	}
}

dependencies {
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'com.thaiopensource:trang:20091111'
	implementation 'net.sourceforge.saxon:saxon:9.1.0.8'
	implementation localGroovy()

	implementation 'io.projectreactor:reactor-core:3.4.6'
	implementation 'gradle.plugin.org.gretty:gretty:3.0.1'
	implementation 'com.apollographql.apollo:apollo-runtime:2.4.5'
	implementation 'com.github.ben-manes:gradle-versions-plugin:0.38.0'
	implementation 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1'
	implementation 'io.spring.gradle:docbook-reference-plugin:0.3.1'
	implementation 'io.spring.gradle:propdeps-plugin:0.0.10.RELEASE'
	implementation 'io.spring.javaformat:spring-javaformat-gradle-plugin:0.0.15'
	implementation 'io.spring.nohttp:nohttp-gradle:0.0.7'
	implementation 'org.asciidoctor:asciidoctor-gradle-jvm:3.1.0'
	implementation 'org.asciidoctor:asciidoctor-gradle-jvm-pdf:3.1.0'
	implementation 'org.hidetake:gradle-ssh-plugin:2.10.1'
	implementation 'org.jfrog.buildinfo:build-info-extractor-gradle:4.9.10'
	implementation 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7.1'

	testImplementation 'junit:junit:4.12'
	testImplementation 'org.apache.commons:commons-io:1.3.2'
	testImplementation 'org.assertj:assertj-core:3.13.2'
	testImplementation 'org.mockito:mockito-core:3.0.0'
	testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
	testImplementation "com.squareup.okhttp3:mockwebserver:3.14.9"
}


test.onlyIf { !project.hasProperty("buildSrc.skipTests") }
