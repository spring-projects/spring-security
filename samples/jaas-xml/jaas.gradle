// JAAS sample build file

apply plugin: 'war'
apply plugin: 'jetty'

def excludeModules = ['spring-security-acl', 'jsr250-api', 'ehcache', 'spring-jdbc', 'spring-tx']

configurations {
    excludeModules.each {name ->
        runtime.exclude module: name
    }

    runtime.exclude group: 'org.aspectj'
}

dependencies {
    providedCompile "org.apache.tomcat:tomcat-servlet-api:$servletApiVersion"

    compile project(':spring-security-core'),
            "org.springframework:spring-beans:$springVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-web:$springVersion"

    runtime project(':spring-security-web'),
            project(':spring-security-config'),
            project(':spring-security-taglibs'),
            "org.springframework:spring-context-support:$springVersion",
            "javax.servlet:jstl:$jstlVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "ch.qos.logback:logback-classic:$logbackVersion"
}

jettyRun {
    contextPath = "/jaas"
}
