def String[] modules = [
    'core',
    'remoting',
    'web',
    'ldap',
    'acl',
    'config',
    'cas',
    'openid',
    'taglibs',
    'aspects',
    'crypto'
]

def String[] samples = [
    'tutorial',
    'contacts',
    'openid',
    'aspectj',
    'gae',
    'dms',
    'preauth',
    'cas/server',
    'cas/sample',
    'ldap',
    'jaas'
]

def String[] itest = [
    'web',
    'context'
]

include modules

modules.each {name ->
    def p = findProject(":${name}")
    p.name = "spring-security-${name}"
    p.buildFileName = "${name}.gradle"
}

include samples

samples.each {name ->
    def p = findProject(":${name}")
    def fullName = name.replaceAll('/','')
    p.name = "spring-security-samples-${fullName}"
    p.buildFileName = "${fullName}.gradle"
    p.projectDir = new File(settingsDir, "samples/${name}");
}

include itest

itest.each { name ->
    def p = findProject(":${name}")
    p.name = "itest-${name}"
    p.buildFileName = "itest-${name}.gradle"
    p.projectDir = new File(settingsDir, "itest/${name}");
}

include 'docs', 'docs:faq', 'docs:manual'

def docs = findProject(':docs')
docs.buildFileName = 'docs.gradle'

rootProject.name = 'spring-security'

rootProject.children.each {project ->
    assert project.projectDir.isDirectory()
    assert project.buildFile.isFile()
}
