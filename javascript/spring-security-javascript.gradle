
/*
 * Copyright 2002-2024 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'base'
    id 'com.github.node-gradle.node' version '7.1.0'
}

node {
    download = true
    version = '20.17.0'
}

tasks.named('check') {
    dependsOn 'npm_run_check'
}

tasks.register('dist', Zip) {
    dependsOn 'npm_run_assemble'
    from 'build/dist/spring-security.js'
    into 'org/springframework/security'
}

configurations {
    javascript {
        canBeConsumed = true
        canBeResolved = false
    }
}

artifacts {
    javascript(project.layout.buildDirectory.dir('dist')) {
        builtBy(npm_run_assemble)
    }
}
