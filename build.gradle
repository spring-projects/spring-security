import io.spring.gradle.IncludeRepoTask
import trang.RncToXsd

buildscript {
	dependencies {
		classpath libs.io.spring.javaformat.spring.javaformat.gradle.plugin
		classpath libs.io.spring.nohttp.nohttp.gradle
		classpath libs.io.freefair.gradle.aspectj.plugin
		classpath libs.org.jetbrains.kotlin.kotlin.gradle.plugin
		classpath libs.com.netflix.nebula.nebula.project.plugin
	}
	repositories {
		maven { url 'https://plugins.gradle.org/m2/' }
	}
}

plugins {
	alias(libs.plugins.org.gradle.wrapper.upgrade)
}

apply plugin: 'io.spring.nohttp'
apply plugin: 'locks'
apply plugin: 'io.spring.convention.root'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'org.springframework.security.versions.verify-dependencies-versions'
apply plugin: 'org.springframework.security.check-expected-branch-version'
apply plugin: 'io.spring.security.release'

group = 'org.springframework.security'
description = 'Spring Security'

ext.snapshotBuild = version.contains("SNAPSHOT")
ext.releaseBuild = version.contains("SNAPSHOT")
ext.milestoneBuild = !(snapshotBuild || releaseBuild)

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/milestone" }
}

springRelease {
	weekOfMonth = 3
	dayOfWeek = 1
	referenceDocUrl = "https://docs.spring.io/spring-security/reference/{version}/index.html"
	apiDocUrl = "https://docs.spring.io/spring-security/site/docs/{version}/api/"
	replaceSnapshotVersionInReferenceDocUrl = true
}

def toolchainVersion() {
	if (project.hasProperty('testToolchain')) {
		return project.property('testToolchain').toString().toInteger()
	}
	return 17
}

subprojects {
	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(toolchainVersion())
		}
	}
	kotlin {
		jvmToolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}
	tasks.withType(JavaCompile).configureEach {
		options.encoding = "UTF-8"
		options.compilerArgs.add("-parameters")
		options.release.set(17)
	}
}

allprojects {
	if (!['spring-security-bom', 'spring-security-docs'].contains(project.name)) {
		apply plugin: 'io.spring.javaformat'
		apply plugin: 'checkstyle'

		pluginManager.withPlugin("io.spring.convention.checkstyle", { plugin ->
			configure(plugin) {
				dependencies {
					checkstyle libs.io.spring.javaformat.spring.javaformat.checkstyle
				}
				checkstyle {
					toolVersion = '8.34'
				}
			}
		})

		if (project.name.contains('sample')) {
			tasks.whenTaskAdded { task ->
				if (task.name.contains('format') || task.name.contains('checkFormat') || task.name.contains("checkstyle")) {
					task.enabled = false
				}
			}
		}
	}
}

develocity {
	buildScan {
		termsOfUseUrl = 'https://gradle.com/help/legal-terms-of-use'
		termsOfUseAgree = 'yes'
	}
}

nohttp {
	source.exclude "buildSrc/build/**", "javascript/.gradle/**", "javascript/package-lock.json", "javascript/node_modules/**", "javascript/build/**", "javascript/dist/**"
	source.builtBy(project(':spring-security-config').tasks.withType(RncToXsd))
}

tasks.named('checkstyleNohttp') {
	maxHeapSize = '1g'
}

tasks.register('cloneRepository', IncludeRepoTask) {
	repository = project.getProperties().get("repositoryName")
	ref = project.getProperties().get("ref")
	var defaultDirectory = project.file("build/tmp/clone")
	outputDirectory = project.hasProperty("cloneOutputDirectory") ? project.file("$cloneOutputDirectory") : defaultDirectory
}

tasks.register('updateCopyrights') {

	def now = Calendar.instance.get(Calendar.YEAR) as String
	def branch = project.hasProperty("branch") ? project.property("branch") : "origin/main"
	def gitOutput = ["git", "diff", "--diff-filter=M", "--name-only", branch, "--", "*.java"].execute().text
	gitOutput.readLines()
			.collect { new File(it) }
			.each { file ->
				def line
				file.withReader { reader ->
					while (line = reader.readLine()) {
						def matcher = line =~ /Copyright (20\d\d)-?(20\d\d)? the original author or authors.$/
						if (matcher.count) {
							def beginningYear = matcher[0][1]
							if (now != beginningYear && now != matcher[0][2]) {
								def sourceCode = file.getText('UTF-8')
								sourceCode = sourceCode.replaceFirst(/20\d\d(-20\d\d)?/, "$beginningYear-$now")
								file.text = sourceCode
								println "Update copyright for file: $file"
							}
						}
					}
				}
			}
}

wrapperUpgrade {
	gradle {
		'spring-security' {
			repo = 'spring-projects/spring-security'
			baseBranch = '6.3.x' // runs only on 6.3.x and the update is merged forward to main
		}
	}
}
